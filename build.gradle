plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.0.0-beta4'
}

subprojects {
    version = project.mod_version
    group = project.maven_group
    plugins.apply 'java'
    plugins.apply 'maven-publish'
    plugins.apply 'com.gradleup.shadow'

    repositories {
        mavenCentral()
        maven { url = uri("https://repo.opencollab.dev/main/") }
        maven { url = uri("https://api.modrinth.com/maven") }
    }

    base {
        archivesName = project.archives_base_name + "-" + project.name
    }
    ext.loadProps = { func ->
        func(
                "mod_id": project.archives_base_name + "-" + project.name,
                "version": project.version,
                "pretty_name": project.pretty_name,
                "authors": project.authors,
                "minecraft_version": project.minecraft_version,
                "floodgate_version": project.floodgate_version,
                "luckperms_version": project.luckperms_version,
                "slf4j_version": project.slf4j_version,
                "toml_version": project.toml_version
        )
    }


    dependencies {
        compileOnly "org.geysermc.floodgate:api:${project.floodgate_version}"
        compileOnly "net.luckperms:api:${project.luckperms_version}"

        compileOnly "org.projectlombok:lombok:${project.lombok_version}"
        annotationProcessor "org.projectlombok:lombok:${project.lombok_version}"

        compileOnly "org.slf4j:slf4j-api:${project.slf4j_version}"

        implementation "com.moandjiezana.toml:toml4j:${project.toml_version}"

        if (project.name != "common") {
            implementation project(':common')
        }
    }

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 21
    }

    java {
        withSourcesJar()

        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    jar {
        from("LICENSE") {
            rename { "${it}_${project.base.archivesName.get()}" }
        }
    }

    shadowJar {
        dependencies {
            include(project(':common'))
            include(dependency("com.moandjiezana.toml:toml4j:${project.toml_version}"))
        }
    }
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

shadowJar {
    dependencies {
        include(project(':common'))
        include(project(':fabric'))
        include(project(':velocity'))
    }
}